// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {

    apply from: 'dependencies.gradle'
    apply from: 'projectDependenciesGraph.gradle'
    apply from: 'defaultConfigAndroid.gradle'

    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:7.2.1'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

allprojects {
    repositories {
        google()
        mavenCentral()
    }

    gradle.projectsEvaluated {
        tasks.withType(JavaCompile) {
            options.compilerArgs << "-Xmaxerrs" << "1000"
        }
        tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
            kotlinOptions {
                jvmTarget = "1.8"
            }
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

subprojects {

    afterEvaluate {
        if (this.hasProperty("android")) {
            android {
                compileSdkVersion compile_sdk_version
                buildToolsVersion build_tools_version

                defaultConfig {
                    minSdkVersion min_sdk_version
                    targetSdkVersion target_sdk_version

                    testInstrumentationRunner = "androidx.test.runner.AndroidJUnitRunner"
                }

                buildTypes {
                    release {
                        minifyEnabled false
                    }
                    debug {
                    }
                    instrumentation {
                    }
                }

                compileOptions {
                    sourceCompatibility JavaVersion.VERSION_1_8
                    targetCompatibility JavaVersion.VERSION_1_8
                }

                kotlinOptions {
                    jvmTarget = '1.8'
                }

                packagingOptions {
                    exclude 'META-INF/DEPENDENCIES'
                    exclude 'META-INF/LICENSE'
                    exclude 'META-INF/LICENSE.txt'
                    exclude 'META-INF/license.txt'
                    exclude 'META-INF/NOTICE'
                    exclude 'META-INF/NOTICE.txt'
                    exclude 'META-INF/notice.txt'
                    exclude 'META-INF/ASL2.0'
                    exclude("META-INF/*.kotlin_module")
                }
            }
        }
    }
}
