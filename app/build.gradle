apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion compile_sdk_version
    buildToolsVersion build_tools_version

    defaultConfig {
        applicationId "com.voidx.github"
        minSdkVersion min_sdk_version
        targetSdkVersion target_sdk_version
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        buildConfigField "String", "SERVER_URL", "\"https://api.github.com/\""

    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {

        }
        instrumentation.initWith(debug)
        instrumentation {
            buildConfigField "String", "SERVER_URL", "\"http://localhost:8080/\""
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    testBuildType "instrumentation"
    testOptions {
        animationsDisabled = true
    }

    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/ASL2.0'
        exclude("META-INF/*.kotlin_module")
    }

    buildFeatures{
        dataBinding = true
        viewBinding = true
    }

}

dependencies {
    implementation project(projectDependencies.coreAndroid)
    implementation project(projectDependencies.utility)

    implementation project(projectDependencies.userPublic)
    implementation project(projectDependencies.userImpl)

    implementation project(projectDependencies.repoPublic)
    implementation project(projectDependencies.repoImpl)

    implementation project(projectDependencies.searchPublic)
    implementation project(projectDependencies.searchImpl)

    implementation project(projectDependencies.pullRequestImpl)
    implementation project(projectDependencies.pullRequestPublic)

    implementation coreDependencies.kotlin
    implementation coreDependencies.dagger
    kapt coreDependencies.daggerCompiler

    implementation viewDependencies.material
    implementation viewDependencies.fragment
    implementation viewDependencies.appCompat
    implementation viewDependencies.lifecycle
    implementation viewDependencies.androidx

    implementation dataDependencies.rxJava
    implementation dataDependencies.retrofit
}